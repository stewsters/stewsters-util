buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}
plugins {
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.7.3"
    id "me.champeau.gradle.jmh" version "0.3.1"
    id "net.saliman.cobertura" version "2.4.0"
}
repositories {
    mavenCentral()
    jcenter()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:all"
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.stewsters.util'
version = '0.17'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile "org.codehaus.groovy:groovy-all:2.4.8"
    testRuntime 'org.slf4j:slf4j-nop:1.7.12' // for cobertura
}

if (!project.hasProperty('bintray_net_user')) {
    project.ext.bintray_net_user = 'default'
}
if (!project.hasProperty('bintray_net_key')) {
    project.ext.bintray_net_key = 'fake'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}


bintray {
    apiUrl = "https://api.bintray.com"

    user = bintray_net_user
    key = bintray_net_key

    publications = ['mavenJava']

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
        name = 'stewsters-util'
        desc = 'A package of Java roguelike utilities.'
        websiteUrl = 'https://github.com/stewsters/stewsters-util'
        issueTrackerUrl = 'https://github.com/stewsters/stewsters-util/issues'
        vcsUrl = 'https://github.com/stewsters/stewsters-util.git'
        licenses = ['MIT']
        labels = ['roguelike', 'utilities']

        publicDownloadNumbers = true

        //Optional version descriptor
        version {
            name = "0.17" //Bintray logical version name
            vcsTag = "0.17"
        }
    }

}

jmh {
    iterations = 5
    fork = 5
    warmupIterations = 5

    jmhVersion = '1.17.3'
}

cobertura {
    coverageFormats = ['html']
    coverageIgnoreTrivial = true
    coverageIgnores = ['org.slf4j.Logger.*']
    coverageReportDir = new File("$buildDir/reports/cobertura")
}